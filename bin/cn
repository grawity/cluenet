#!/usr/bin/env perl
BEGIN { push @INC, ($ENV{CLUENET} // "$ENV{HOME}/cluenet")."/lib/perl5" }
use warnings;
use strict;
use feature "say";
use feature "switch";

use Authen::SASL;
use Cluenet::LDAP;
use Data::Dumper;
use Getopt::Long qw(:config posix_default no_ignore_case);
use Net::DNS;
use Net::IP;
use Net::LDAP;
use Net::LDAP::Extension::WhoAmI;
use Net::LDAP::Util qw(ldap_explode_dn);
use Socket;
use Text::Format;

my $ldap;
my $cmd;
my $exit;
my $my_name;
my %commands;

use constant SHELL_SERVICES => qw(atd cron login passwd sshd su sudo);

$ENV{LDAPURI} = "ldap://ldap.cluenet.org";
$ENV{LDAPBASE} = "dc=cluenet,dc=org";

### String and hostname manipulation

sub lookup_host {
	my ($host) = @_;
	my @addrs = ();
	my $r = Net::DNS::Resolver->new;

	my $query = $r->query($host, "A");
	if ($query) { push @addrs, $_->address for $query->answer }

	$query = $r->query($host, "AAAA");
	if ($query) { push @addrs, $_->address for $query->answer }

	return @addrs;
}

sub format_name {
	my ($user) = @_;
	my $name = $user->{uid};
	if ($user->{cn} ne $user->{uid}) {
		$name .= " (".$user->{cn}.")";
	}
	return $name;
}

sub format_address {
	my ($host, $port) = @_;
	if ($host =~ /:/) {
		$host = Net::IP::ip_compress_address($host, 6);
		return "[$host]:$port";
	} else {
		return "$host:$port";
	}
}

# Split "host/service" arg
sub to_hostservice {
	my ($host, $service, $rest) = split /\//, shift, 3;
	return (make_server_fqdn($host), $service, $rest);
}
sub to_hostservice_safe {
	my ($host, $service, $rest) = split /\//, shift, 3;
	unless ($host and $service) {
		die "syntax error: empty host or service: '$host/$service'\n";
	}
	return (make_server_fqdn($host), $service, $rest);
}

sub str_bool {
	given (shift) {
		return 1 if !defined $_;
		return 1 if /^(1|y|yes|true)$/i;
		return 0 if /^(0|n|no|false)$/i;
		die "Invalid value '$_'\n";
	}
}
sub ldap_bool { str_bool(shift) ? "TRUE" : "FALSE" }

# Output fields

my $_rows = 0;
sub row {
	my ($label, $value) = @_;
	my @rowvalue = ();
	my $rowlength = 0;
	for my $item (split / /, $value) {
		if ($rowlength + length($item) > 62) {
			printf "%-16s%s\n", $label, join(" ", @rowvalue);
			++$_rows;
			$label = "";
			@rowvalue = ();
			$rowlength = 0;
		} else {
			push @rowvalue, $item;
			$rowlength += length($item) + 1;
		}
	}
	if (@rowvalue) {
		printf "%-16s%s\n", $label, join(" ", @rowvalue);
		++$_rows;
	}
}
sub row_r { printf "%-16s%s\n", @_; ++$_rows; }
sub endsection { if ($_rows) {print "\n"; $_rows = 0;} }

### LDAP connection

sub connect_auth { goto &Cluenet::LDAP::ldap_connect_auth; }
sub connect_anon { goto &Cluenet::LDAP::ldap_connect_anon; }

# Get and cache LDAP authzid
sub whoami {
	if (!defined $my_name) {
		$my_name = $ldap->who_am_i->response;
		$my_name =~ s/^u://;
		$my_name =~ s/^dn:uid=(.+?),.*$/$1/;
		# cross-realm
		$my_name =~ s/\@nullroute\.eu\.org$//;
	}
	return $my_name;
}

# Call `ldapsearch` to dump raw LDIF
sub ext_dump_ldif {
	my (%args) = @_;
	my @cmd = ("ldapsearch", "-QLLL");
	push @cmd, "-s", $args{scope}		if $args{scope};
	push @cmd, "-b", $args{base}		if $args{base};
	push @cmd, $args{filter}		if $args{filter};
	push @cmd, @{$args{attributes}}		if $args{attributes};
	system(@cmd);
}

sub get_server_info {
	my ($host, $is_dn) = @_;
	my $dn = $is_dn ? $host : server_dn($host);

	my $res = $ldap->search(base => $dn, scope => "base",
		filter => q(objectClass=server));
	$res->is_error and die ldap_errmsg($res, $dn);
	for my $entry ($res->entries) {
		my %server = map {$_ => [$entry->get_value($_)]} $entry->attributes;
		for (qw(cn owner internalAddress sshPort)) {
			$server{$_} = $server{$_}->[0];
		}
		for (qw(isActive isOfficial userAccessible)) {
			$server{$_} = $server{$_}->[0] eq "TRUE";
		}
		$server{address} = [lookup_host($server{cn})];
		return \%server;
	}
}

sub print_server_info {
	my ($server, $owner, @admins) = @_;
	@admins = sort {$a->{uid} cmp $b->{uid}} @admins;
	my $port = $server->{sshPort} // 22;

	row "HOSTNAME:"		=> uc $server->{cn};
	row "address:"		=> format_address($_, $port)
		for @{$server->{address}};
	row "owner:"		=> format_name($owner);
	row "admin:"		=> format_name($_)
		for @admins;
	row "status:"		=> join(", ", grep {defined} (
					$server->{isOfficial}     ? "official" : "unofficial",
					$server->{userAccessible} ? "public"   : "private",
					$server->{isActive}       ? "active"   : "inactive",
					(grep {/:/} @{$server->{address}}) ? "IPv6" : undef,
				));
	if (defined $server->{authorizedService}) {
		my @services = sort @{$server->{authorizedService}};
		row "services:"	=> join(", ", @services);
	}
	endsection;

	my $fmt = Text::Format->new(leftMargin => 4, firstIndent => 0);

	if ($server->{description}) {
		print $fmt->format(@{$server->{description}}), "\n";
	}

	if ($server->{serverRules}) {
		if ($server->{serverRules}[0] ne 'default') {
			print $fmt->format(@{$server->{serverRules}}), "\n";
		}
	}
}

sub get_user_info {
	my ($user, $is_dn) = @_;
	my $dn = $is_dn ? $user : user_dn($user);

	my $res = $ldap->search(base => $dn, scope => "base",
		filter => q(objectClass=posixAccount));
	$res->is_error and die ldap_errmsg($res, $dn);
	for my $entry ($res->entries) {
		my %user = map {$_ => [$entry->get_value($_)]} $entry->attributes;
		for (qw(uid uidNumber gidNumber gecos homeDirectory loginShell cn
				krb5PrincipalName clueIrcNick ircServicesUser)) {
			$user{$_} = $user{$_}->[0];
		}
		$user{cn} =~ s/^\s+|\s+$//g;
		if (defined $user{altEmail}) {
			print Dumper($user{altEmail});
			push @{$user{mail}}, @{$user{altEmail}};
		}
		return \%user;
	}
}

sub print_user_info {
	my ($user) = @_;
	row "PERSON:"		=> format_name($user);
	row "uid:"		=> $user->{uidNumber};
	row "shell:"		=> $user->{loginShell} // "(unset)";
	row "IRC account:"	=> $user->{ircServicesUser}		if $user->{ircServicesUser};
	row "email:"		=> join(", ", @{$user->{mail}})		if $user->{mail};
	endsection;
}

### Miscellaneous

sub usage {
	print STDERR "Usage: @_\n";
	exit 2;
}

### User interface commands

$commands{"access"} = sub {
	my (@services, @add, @del);

	for (@_) {
		if (m!/!) {
			my ($h, $s) = to_hostservice_safe($_);
			if ($s eq 'shell') {
				push @services, [$h, $_] for SHELL_SERVICES;
			} else {
				push @services, [$h, $s];
			}
		}
		elsif (m!^\+(.+)$!) {
			push @add, $1;
		}
		elsif (m!^-(.+)$!) {
			push @del, $1;
		}
		else {
			warn "invalid argument: $_\n";
		}
	}

	usage("access <host/service>... [+-]<user>...") unless @services;

	if (@add or @del) {
		$ldap = connect_auth;
		for (@services) {
			my ($host, $service) = @$_;
			my $group = "cn=$service,cn=svcAccess,".server_dn($host);

			my $res = $ldap->search(base => $group, scope => "base",
				filter => q(objectClass=*), attrs => ["member"]);
			if ($res->is_error) {
				warn ldap_errmsg($res, $group);
				next;
			}

			my (@users, @add_svc, @del_svc, %changes);
			for my $entry ($res->entries) {
				@users = map {user_from_dn $_} $entry->get_value("member");
			}
			@add_svc = grep {not $_ ~~ @users} @add;
			@del_svc = grep {$_ ~~ @users} @del;

			if (@add_svc) {
				$changes{add} = {
					member => [map {user_dn $_} @add_svc],
				};
				print "$host/$service: Adding: ".join(", ", @add_svc)."\n";
			}
			if (@del_svc) {
				$changes{delete} = {
					member => [map {user_dn $_} @del_svc],
				};
				print "$host/$service: Removing: ".join(", ", @del_svc)."\n";
			}

			if (@add_svc or @del_svc) {
				my $res = $ldap->modify($group, %changes);
				$res->is_error and warn ldap_errmsg($res, $group);
			} else {
				print "$host/$service: Nothing to do\n";
			}
		}
	} else {
		$ldap = connect_anon;
		for (@services) {
			my ($host, $service) = @$_;
			my $group = "cn=$service,cn=svcAccess,".server_dn($host);

			my $res = $ldap->search(base => $group, scope => "base",
				filter => q(objectClass=*), attrs => ["member"]);
			if ($res->is_error) {
				warn ldap_errmsg($res, $group);
				next;
			}

			for my $entry ($res->entries) {
				my @users = sort map {user_from_dn $_}
						$entry->get_value("member");
				print "$host/$service\t$_\n" for @users;
			}
		}
	}
};

$commands{"acl"} = sub {
	usage("acl <host>... [+-]<service>...") unless @_;


	my (@hosts, @add, @del);
	my $err = 0;

	for (@_) {
		if (m!^\+(.+)$!) {
			push @add, $1;
		}
		elsif (m!^-(.+)$!) {
			push @del, $1;
		}
		else {
			push @hosts, $_;
		}
	}

	if (@add or @del) {
		$ldap = connect_auth;
		for my $host (@hosts) {
			for my $service (@add) {
				my ($res, $dn, %entry);

				print "Creating ACL: $host/$service\n";

				$dn = "cn=$service,cn=svcAccess,".server_dn($host);
				%entry = (
					objectClass => ["groupOfNames"],
					cn => $service,
					member => [user_dn(whoami)],
				);
				$res = $ldap->add($dn, attr => [%entry]);
				if ($res->is_error) { warn ldap_errmsg($res, $dn); $err++; }

				$dn = server_dn($host);
				%entry = (
					authorizedService => $service,
				);
				$res = $ldap->modify($dn, add => \%entry);
				if ($res->is_error) { warn ldap_errmsg($res, $dn); $err++; }
			}
			for my $service (@del) {
				my ($dn, $res, %entry);

				print "Deleting ACL: $host/$service\n";

				$dn = "cn=$service,cn=svcAccess,".server_dn($host);
				$res = $ldap->delete($dn);
				if ($res->is_error) { warn ldap_errmsg($res, $dn); $err++; }

				$dn = server_dn($host);
				%entry = (
					authorizedService => $service,
				);
				$res = $ldap->modify($dn, delete => \%entry);
				if ($res->is_error) { warn ldap_errmsg($res, $dn); $err++; }
			}
		}
	}
	else {
		$ldap = connect_anon;
		for my $host (@hosts) {
			# authz = authorizedService attributes
			# acl = cn=svcAccess subentry
			my ($res, %services, @svcs_authz, @svcs_acl);

			my $dn = server_dn($host);
			$res = $ldap->search(base => $dn, scope => "base",
				filter => q(objectClass=*), attrs => ["authorizedService"]);
			if ($res->is_error) { warn ldap_errmsg($res, $dn); $err++; }

			for my $entry ($res->entries) {
				push @svcs_authz, $entry->get_value("authorizedService");
			}

			$dn = "cn=svcAccess,".server_dn($host);
			$res = $ldap->search(base => $dn, scope => "one",
				filter => q(objectClass=groupOfNames), attrs => ["cn"]);
			if ($res->is_error) { warn ldap_errmsg($res, $dn); $err++; }

			for my $entry ($res->entries) {
				push @svcs_acl, $entry->get_value("cn");
			}

			# create a hash of $service => [has_authz, has_acl]
			%services = map {$_ => [1, undef]} @svcs_authz;
			$services{$_}->[1] = 1 for @svcs_acl;

			for my $svc (sort keys %services) {
				my ($has_authz, $has_acl) = @{$services{$svc}};
				if (!$has_authz) {
					warn "warning: service '$svc' not listed as authorizedService\n";
				}
				if (!$has_acl) {
					warn "warning: service '$svc' does not have an ACL\n";
				}
				print "$host/$svc\n";
			}
		}
	}

	return $err;
};

$commands{"server"} = sub {
	usage("server <host>...") unless @_;

	$ldap = connect_anon;

	for (@_) {
		my $server = get_server_info($_);
		my $owner = get_user_info($server->{owner}, 1);
		my @admins = map {get_user_info($_, 1)}
				grep {!m|/|} @{$server->{authorizedAdministrator}};
		print_server_info($server, $owner, @admins);
	}
};

$commands{"server:dump"} = sub {
	ext_dump_ldif(base => server_dn($_), scope => "sub") for @_;
	return 0;
};

$commands{"server:list"} = sub {
	$ldap = connect_anon;

	my $base = "ou=servers,dc=cluenet,dc=org";
	my $res = $ldap->search(base => $base, scope => "one",
		filter => q(objectClass=server), attrs => ["cn"]);
	if ($res->is_error) { die ldap_errmsg($res, $base); }

	my @hosts = sort grep {s/\.cluenet\.org$//i}
				map {($_->get_value("cn"))[0]}
				$res->entries;

	print "$_\n" for @hosts;
};

$commands{"server:admin"} = sub {
	my (@hosts, @add, @del);

	my $show_princ = 0;

	Getopt::Long::GetOptionsFromArray(\@_,
		"p|principal" => \$show_princ);

	for (@_) {
		when (/^\+(.+)$/)	{ push @add, user_from_dn($1) }
		when (/^-(.+)$/)	{ push @del, user_from_dn($1) }
		default			{ push @hosts, $_ }
	}

	usage("server:admin <host>... [+-]<user>...") unless @hosts;

	if (@add or @del) {
		# update admins
		$ldap = connect_auth;
		for my $host (@hosts) {
			my $dn = server_dn($host);
			my $res = $ldap->search(base => $dn, scope => "base",
				filter => q(objectClass=server),
				attrs => ["authorizedAdministrator", "owner"]);
			if ($res->is_error) {
				warn ldap_errmsg($res, $dn);
				next;
			}

			my ($owner, @admins, %changes);
			for my $entry ($res->entries) {
				$owner = user_from_dn($entry->get_value("owner"));
				@admins = map {user_from_dn $_}
					$entry->get_value("authorizedAdministrator");
			}

			if ($owner ~~ @del and !($owner ~~ @admins)) {
				warn "$host: Cannot remove server owner $owner\n";
			}

			my @add_host = grep {not $_ ~~ @admins} @add;
			my @del_host = grep {$_ ~~ @admins} @del;

			if ($owner ~~ @add_host) {
				warn "$host: Adding server owner $owner as an admin\n";
			}

			if (@add_host) {
				$changes{add} = {
					authorizedAdministrator => [map {user_dn $_} @add_host],
				};
				print "$host: Adding ".join(", ", @add_host)."\n";
			}
			if (@del_host) {
				$changes{delete} = {
					authorizedAdministrator => [map {user_dn $_} @del_host],
				};
				print "$host: Removing ".join(", ", @del_host)."\n";
			}

			if (@add_host or @del_host) {
				my $res = $ldap->modify($dn, %changes);
				$res->is_error and warn ldap_errmsg($res, $dn);
			} else {
				print "$host: Nothing to do\n";
			}
		}
	} else {
		# list admins
		$ldap = connect_anon;
		for my $host (@hosts) {
			my $dn = server_dn($host);
			my $res = $ldap->search(base => $dn, scope => "base",
				filter => q(objectClass=server),
				attrs => ["authorizedAdministrator", "owner"]);
			if ($res->is_error) {
				warn ldap_errmsg($res, $dn);
				next;
			}

			for my $entry ($res->entries) {
				my $fmt = "%-15s\t%s\n";
				my $owner = $entry->get_value("owner");
				my @admins = $entry->get_value("authorizedAdministrator");

				$owner = user_from_dn $owner;
				@admins = sort map {user_from_dn $_} @admins;

				if ($show_princ) {
					$owner =~ s/$/\@CLUENET.ORG/;
					@admins =
						grep {/@/ or s/$/\@CLUENET.ORG/}
						grep {s/(@)(.+)/$1.uc($2)/e or 1}
						@admins;
				}

				printf $fmt, $host, $owner.' (owner)';
				printf $fmt, $host, $_ for @admins;
			}
		}
	}
};

$commands{"server:create"} = sub {
	my ($res, $err, %entry);

	my $host = shift;
	my $owner;

	Getopt::Long::GetOptionsFromArray(\@_,
		"o|owner=s" => \$owner);

	usage("server:create <hostname>") unless $host;

	$ldap = connect_auth;
	$owner //= whoami;

	my $fqdn = make_server_fqdn($host);
	print "Creating server: $fqdn (owned by $owner)\n";

	# add main server entry
	my $dn = server_dn($host);
	%entry = (
		objectClass => ["server",
			"ipHost",
			"authorizedServiceObject",
			"serviceRequirementObject"],
		cn => $fqdn,
		owner => user_dn($owner),
		isActive => "TRUE",
		isOfficial => "FALSE",
		userAccessible => "TRUE",
		authorizedService => [SHELL_SERVICES],
		ipHostNumber => "0.0.0.0",
	);
	print "+ $dn\n";
	$res = $ldap->add($dn, attr => [%entry]);
	if ($res->is_error) { die ldap_errmsg($res, $dn); }

	# add svcAccess subtree
	my $subdn = "cn=svcAccess,$dn";
	%entry = (
		objectClass => ["groupOfGroups"],
		cn => "svcAccess",
	);
	print "+ $subdn\n";
	$res = $ldap->add($subdn, attr => [%entry]);
	if ($res->is_error) { warn ldap_errmsg($res, $dn); $err++; }

	# add service groups
	for (SHELL_SERVICES) {
		$subdn = "cn=$_,cn=svcAccess,$dn";
		%entry = (
			objectClass => ["groupOfNames"],
			cn => $_,
			member => user_dn($owner),
		);
		print "+ $subdn\n";
		$res = $ldap->add($subdn, attr => [%entry]);
		if ($res->is_error) { warn ldap_errmsg($res, $dn); $err++; }
	}

	return $err;
};

$commands{"server:delete"} = sub {
	my ($res, $err);

	usage("server:delete <hostname>...") unless @_;

	$ldap = connect_auth;

	for my $host (@_) {
		my $fqdn = make_server_fqdn($host);
		print "Deleting server: $fqdn\n";

		my $dn = server_dn($fqdn);
		$res = $ldap->search(base => $dn, scope => "sub",
			filter => q(objectClass=*), attributes => ["dn"]);
		$res->is_error and die ldap_errmsg($res, $dn);

		my @children = reverse map {$_->dn} $res->entries;
		for my $subdn (@children) {
			print "- $subdn\n";
			$res = $ldap->delete($subdn);
			if ($res->is_error) { warn ldap_errmsg($res, $dn); $err++; }
		}
	}

	return $err;
};

$commands{"server:set"} = sub {
	my $err;
	usage("server:set", qw(server attr=value ...)) unless @_;
	my @hosts = split(/,/, shift);
	my %changes = (replace => {});

	for (@_) {
		my ($attr, $value) = split(/=/, $_, 2);
		given ($attr) {
			when ("active") {
				$attr = "isActive";
				$value = ldap_bool($value);
			}
			when ("official") {
				$attr = "isOfficial";
				$value = ldap_bool($value);
			}
			when ("public") {
				$attr = "userAccessible";
				$value = ldap_bool($value);
			}
			when ("owner") {
				$value = user_dn($value);
			}
		}
		$changes{replace}{$attr} = [$value];
	}

	$ldap = connect_auth;
	for my $host (@hosts) {
		my $dn = server_dn($host);
		print "Modifying $dn\n";
		my $res = $ldap->modify($dn, %changes);
		$res->is_error and warn ldap_errmsg($res, $dn);
	}
};

$commands{"user"} = sub {
	$ldap = connect_anon;
	for my $user (@_) {
		print_user_info(get_user_info($user));
	}
	return 0;
};

$commands{"user:getdn"} = sub {
	print user_dn($_)."\n" for @_;
	return 0;
};

$commands{"user:dump"} = sub {
	ext_dump_ldif(base => user_dn($_), scope => "sub") for @_;
	return 0;
};

$commands{"user:acs"} = sub {
	if (@_) {
		my %changes = ();
		for (@_) {
			my ($key, $value) = /^(.+)=(\w+)?$/
				or die "syntax error: $_\n";
			$value =~ /^(anon|user|none|default)?$/
				or die "error: valid values are: anon user none default\n";
			$key = "acs".$key;
			$changes{lc $key} = $value || "default";
		}

		my (%replace, @delete);
		for my $key (keys %changes) {
			if ($changes{$key} eq 'default') {
				print "Deleting $key\n";
				push @delete, $key;
			} else {
				print "Setting $key to '$changes{$key}'\n";
				$replace{$key} = $changes{$key};
			}
		}

		$ldap = connect_auth;
		my $dn = user_dn(whoami);
		my $res = $ldap->modify($dn, replace => \%replace,
			delete => \@delete);
		if ($res->is_error) { die ldap_errmsg($res, $dn); }
		return 0;
	}
	else {
		$ldap = connect_auth;
		my $dn = user_dn(whoami);
		my $res = $ldap->search(base => $dn, scope => "base",
			filter => q(objectClass=posixAccount));
		$res->is_error and die ldap_errmsg($res, $dn);
		for my $entry ($res->entries) {
			for my $attr (sort grep {/^acs/} $entry->attributes) {
				my $value = $entry->get_value($attr);
				$attr =~ s/^acs//;
				print "$attr: $value\n";
			}
		}
	}
};

$commands{"user:chsh"} = sub {
	my $shell = shift;
	if ($shell && $shell eq '-s') { $shell = shift; }

	if (defined $shell) {
		if (!$shell) {
			die "error: shell not specified\n";
		}
		elsif ($shell !~ m!^/.+!) {
			die "error: shell must be an absolute filesystem path\n";
		}
		elsif ($shell !~ m!^/bin/(ba)?sh$!) {
			warn "warning: shell might not be available on other servers\n";
		}

		$ldap = connect_auth;
		my $dn = user_dn(whoami);
		my %entry = (loginShell => $shell);

		print "Changing shell for ".whoami." to $shell\n";
		my $res = $ldap->modify($dn, replace => \%entry);
		$res->is_error and die ldap_errmsg($res, $dn);
	}
	else {
		$ldap = connect_auth;
		my $dn = user_dn(whoami);

		my $res = $ldap->search(base => $dn, scope => "base",
			filter => q(objectClass=posixAccount));
		$res->is_error and die ldap_errmsg($res, $dn);

		for my $entry ($res->entries) {
			$shell = $entry->get_value("loginShell");
		}
		$shell //= "not set.";
		print "Your current shell is $shell\n";
	}
};

$commands{"help"} = sub {
	print qq{Usage: cluenet <command>

ACCESS LISTS
	show	access <host/service>...
	grant	access <host/service>... [+-]<user>...
	list	acl <host>...
	create	acl <host>... [+-]<user>...

USER ACCOUNTS
	view	user <user>...
	shell	user:chsh <shell>
	privacy	user:acs [<attribute=value>...]

SERVERS
	view	server <host>
	admins	server:admin <host>... [[+-]<user>...]
	add	server:create <host> [--owner <owner>]

MISCELLANEOUS
	whoami
};

	return 0;
};

$commands{"whoami"} = sub {
	$ldap = connect_auth();
	my $dn = $ldap->who_am_i->response;
	my $uid;
	given ($dn) {
		when (/^dn:uid=(.+),ou=people,dc=cluenet,dc=org$/) {
			$uid = $1;
		}
	}
	say $dn;
	given ($uid) {
		when (/^(.+)@(.+?)$/) {
			my $query = Net::DNS::Resolver->new->query("_kerberos.$2", "TXT");
			if ($query) {
				my $realm = (($query->answer)[0]->char_str_list)[0];
				say "Foreign user '$1' from realm $realm";
			}
			else {
				say "Foreign user '$1' from $2";
			}
		}
		default {
			say "Cluenet user '$uid'";
		}
	}
	return 0;
};

### Main code

$cmd = shift(@ARGV) // "help";

if (defined $commands{$cmd}) {
	$exit = $commands{$cmd}->(@ARGV);
	$ldap and $ldap->unbind;
	exit (defined $exit && $exit) ? 1 : 0;
} else {
	die "Unknown command '$cmd'\n";
}
