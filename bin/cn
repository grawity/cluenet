#!/usr/bin/env perl
use utf8;
BEGIN { push @INC, ($ENV{CLUENET} // "$ENV{HOME}/cluenet")."/lib/perl5" }
use warnings;
use strict;
use feature qw(say state switch);

use constant ERR_SYNTAX => 2;

use Authen::SASL;
use Cluenet::Common qw(
	make_server_fqdn
);
use Cluenet::LDAP;
use Data::Dumper;
use Getopt::Long qw(:config posix_default no_ignore_case);
use Net::DNS;
use Net::IP;
use Net::LDAP;
use Net::LDAP::Extension::WhoAmI;
use Net::LDAP::Util qw(ldap_explode_dn);
use POSIX qw(strftime);
use POSIX::strptime;
use Socket;
use Text::Format;

my $ldap;
my $cmd;
my $exit;
my %commands;

use constant SHELL_SERVICES => qw(atd cron login passwd sshd su sudo);

$ENV{LDAPURI} = "ldap://ldap.cluenet.org";
$ENV{LDAPBASE} = "dc=cluenet,dc=org";

sub status {
	print STDERR "\r\033[K", shift; $|++;
}

sub done {
	print STDERR "\r\033[K"; $|++;
}

### String and hostname manipulation

sub lookup_host {
	my ($host) = @_;
	my @addrs = ();
	my $r = Net::DNS::Resolver->new;

	my $query = $r->query($host, "A");
	if ($query) { push @addrs, $_->address for $query->answer }

	$query = $r->query($host, "AAAA");
	if ($query) { push @addrs, $_->address for $query->answer }

	return @addrs;
}

sub format_date {
	my ($sz) = @_;
	($sz) = @$sz;
	my (@tm) = POSIX::strptime($sz, "%Y%m%d%H%M%SZ");
	return POSIX::strftime("%Y-%m-%d", @tm);
	# TODO: relative date
}

sub format_name {
	my ($user) = @_;
	my $name = $user->{uid};
	if (defined $user->{cn}) {
		if ($user->{cn} ne $user->{uid}) {
			$name .= " (".$user->{cn}.")";
		}
	}
	elsif (defined $user->{gecos}) {
		my ($gecos) = split(/,/, $user->{gecos});
		if ($gecos ne $user->{uid}) {
			$name .= " (".$gecos.")";
		}
	}
	return $name;
}

sub format_address {
	my ($host, $port) = @_;
	if (ref $port eq 'ARRAY') {
		$port = @$port > 1
			? "{".join(",", @$port)."}"
			: $port->[0];
	}
	if ($host =~ /:/) {
		$host = Net::IP::ip_compress_address($host, 6);
		return "[$host]:$port";
	} else {
		return "$host:$port";
	}
}

sub parse_hostservice {
	my ($host, $service, $rest) = split /\//, shift, 3;
	return (make_server_fqdn($host), $service, $rest);
}

sub parse_hostservice_safe {
	my ($host, $service, $rest) = split /\//, shift, 3;
	unless ($host and $service) {
		die "syntax error: empty host or service: '$host/$service'\n";
	}
	return (make_server_fqdn($host), $service, $rest);
}

sub str_bool {
	given (shift) {
		return 1 if !defined $_;
		return 1 if /^(1|y|yes|true)$/i;
		return 0 if /^(0|n|no|false)$/i;
		die "Invalid value '$_'\n";
	}
}
sub ldap_bool { str_bool(shift) ? "TRUE" : "FALSE" }

# Output fields

my $_rows = 0;
sub row {
	my ($label, $value) = @_;
	if (ref $value eq 'ARRAY') {
		row($label, $_) for @$value;
		return;
	}
	if (!defined $value) {
		$value = '(nil)';
	}
	my @rowvalue = ();
	my $rowlength = 0;
	for my $item (split / /, $value) {
		if ($rowlength + length($item) > 62) {
			printf "%-16s%s\n", $label, join(" ", @rowvalue);
			++$_rows;
			$label = "";
			@rowvalue = ();
			$rowlength = 0;
		} else {
			push @rowvalue, $item;
			$rowlength += length($item) + 1;
		}
	}
	if (@rowvalue) {
		printf "%-16s%s\n", $label, join(" ", @rowvalue);
		++$_rows;
	}
}
sub row_r { printf "%-16s%s\n", @_; ++$_rows; }
sub endsection { if ($_rows) {print "\n"; $_rows = 0;} }

### LDAP connection

sub ldap_connect_auth {
	status "Connecting to LDAP server";
	my $r = Cluenet::LDAP::ldap_connect_auth;
	done;
	return $r;
}

sub ldap_connect_anon {
	status "Connecting to LDAP server";
	my $r = Cluenet::LDAP::ldap_connect_anon;
	done;
	return $r;
}

# Get and cache LDAP authzid
sub whoami {
	state $me;
	if (!defined $me) {
		$me = $ldap->who_am_i->response;
		$me =~ s/^u://;
		$me =~ s/^dn:uid=(.+?),.*$/$1/;
	}
	return $me;
}

# Call `ldapsearch` to dump raw LDIF
sub ext_dump_ldif {
	my (%args) = @_;
	my @cmd = ("ldapsearch", "-QLLL");
	push @cmd, "-s", $args{scope}		if $args{scope};
	push @cmd, "-b", $args{base}		if $args{base};
	push @cmd, $args{filter}		if $args{filter};
	push @cmd, @{$args{attributes}}		if $args{attributes};
	system(@cmd);
}

sub get_server_info {
	state %cache;
	my ($host) = @_;
	my $dn = server_dn($host);
	if (!exists $cache{$dn}) {
		status "Looking up server: ".server_from_dn($dn);
		my $res = $ldap->search(
				base => $dn,
				scope => "base",
				filter => q(objectClass=server),
				attrs => ["*", "+"]);
		ldap_check($res, $dn)
			or return $cache{$dn} = undef;
		for my $entry ($res->entries) {
			my %server = map {$_ => [$entry->get_value($_)]}
					$entry->attributes;
			for (qw(cn owner)) {
				$server{$_} = $server{$_}->[0];
			}
			for (qw(isActive isOfficial userAccessible)) {
				$server{$_} = $server{$_}
						? $server{$_}->[0] eq "TRUE"
						: 0;
			}
			$server{address} = [lookup_host($server{cn})];
			$cache{$dn} = \%server;
			last;
		}
		done;
	}
	return $cache{$dn};
}

sub get_user_name {
	my ($user) = @_;
	my $i = get_user_info($user);
	return format_name($i) if $i;
	my $uid = user_from_dn($user);
	if ($uid =~ /@/) {
		return "\"$uid\" [foreign]";
	} else {
		return "\"$uid\" [missing]";
	}
}

sub print_server_info {
	my ($host) = @_;

	my $server = get_server_info($host);
	my $owner = get_user_info($server->{owner});

	my @admins = sort map {get_user_name($_)}
			grep {!m|/|} @{$server->{admin}};

	my $port = $server->{sshPort} // 22;

	row "HOSTNAME:"		=> uc $server->{cn};
	row "created:"		=> format_date($server->{createTimestamp});
	row "address:"		=> format_address($_, $port)
		for @{$server->{address}};
	row "owner:"		=> format_name($owner);
	row "admin:"		=> \@admins;
	row "status:"		=> join(", ", grep {defined} (
					$server->{isOfficial}     ? "official" : "unofficial",
					$server->{userAccessible} ? "public"   : "private",
					$server->{isActive}       ? "active"   : "inactive",
					(grep {/:/} @{$server->{address}}) ? "IPv6" : undef,
				));
	if (defined $server->{authorizedService}) {
		my @services = sort @{$server->{authorizedService}};
		row "services:"	=> join(", ", @services);
	}
	endsection;

	my $fmt = Text::Format->new(leftMargin => 4, firstIndent => 0);

	if ($server->{description}) {
		print $fmt->format(@{$server->{description}}), "\n";
	}

	if ($server->{serverRules}) {
		if ($server->{serverRules}[0] ne 'default') {
			print $fmt->format(@{$server->{serverRules}}), "\n";
		}
	}
}

sub get_user_info {
	state %cache;
	my ($user) = @_;
	my $dn = user_dn($user);
	if (!exists $cache{$dn}) {
		status "Looking up user: ".user_from_dn($dn);
		my $res = $ldap->search(
			base => $dn,
			scope => "base",
			filter => q(objectClass=posixAccount),
			attrs => ["*", "+"]);
		ldap_check($res, $dn)
			or return $cache{$dn} = undef;
		for my $entry ($res->entries) {
			my %user = map {$_ => [$entry->get_value($_)]} $entry->attributes;
			for (qw(uid uidNumber gidNumber gecos homeDirectory loginShell cn)) {
				$user{$_} = $user{$_}->[0];
			}
			$user{cn} =~ s/^\s+|\s+$//g;
			$cache{$dn} = \%user;
			last;
		}
		done;
	}
	return $cache{$dn};
}

sub print_user_info {
	my ($user) = @_;
	row "PERSON:"		=> format_name($user);
	row "created:"		=> format_date($user->{createTimestamp});
	row "uid:"		=> $user->{uidNumber};
	row "shell:"		=> $user->{loginShell} // "(unset)";
	row "IRC account:"	=> $user->{ircServicesUser}		if $user->{ircServicesUser};
	row "email:"		=> join(", ", @{$user->{mail}})		if $user->{mail};
	row "URL:"		=> $user->{labeledURI}			if $user->{labeledURI};
	endsection;
}

my $err = 0;

sub ldap_exit {
	exit $err ? 1 : 0;
}

sub ldap_check {
	my ($res, $dn, %args) = @_;
	if ($res->is_error) {
		++$err;
		done;
		print STDERR "\e[33m".ldap_errmsg($res, $dn)."\e[m";
		if ($args{die}) {
			ldap_exit;
		}
		return 0;
	} else {
		return 1;
	}
}

# parse "attr=value", "attr+=value", ... list of changes into a LDAP modify operation

sub parse_changelist {
	my ($args, %opts) = @_;
	my %changes;
	my %attrs;

	for (@$args) {
		my @m = /^(\w+)(=|\+=|-=)(.*)$/;
		my ($attr, $op, $value) = @m;
		if ($opts{translate}) {
			($attr, $value) = $opts{translate}->($attr, $value);
		}
		given ($op) {
			# TODO: support "delete attribute" op
			when ("=") {
				push @{$changes{replace}{$attr}}, $value;
				$attrs{$attr}{replace}++;
			}
			when ("+=") {
				push @{$changes{add}{$attr}}, $value;
				$attrs{$attr}{add}++;
			}
			when ("-=") {
				push @{$changes{delete}{$attr}}, $value;
				$attrs{$attr}{delete}++;
			}
		}
	}
	for my $attr (keys %attrs) {
		my $n = $attrs{$attr};
		if (($n->{add} || $n->{delete}) && $n->{replace}) {
			warn "Error: It does not make sense to combine '+=' or '-=' operators with '=' for\n";
			warn "  the same attribute, as \"replace\" will take priority over the other two.\n";
			exit ERR_SYNTAX;
		}
	}
	return \%changes;
}

# apply a LDAP modify operation to several objects

sub modify_objects {
	my ($changes, @objects) = @_;

	my $ldap = ldap_connect_auth;
	for my $dn (@objects) {
		say "Modifying $dn";
		my $res = $ldap->modify($dn, %$changes);
		ldap_check($res, $dn);
	}
}

### Miscellaneous

sub usage {
	print STDERR "Usage: @_\n";
	exit 2;
}

### User interface commands

sub dn_for_user {
	return user_dn(shift);
}

sub dn_for_server {
	return server_dn(shift);
}

sub dn_for_service_acl {
	my ($host, $service) = @_;
	return "cn=".$service.",cn=svcAccess,".dn_for_server($host);
}

sub mgmt_access_get_users {
	my ($ldap, $host, $service) = @_;
	my $dn = dn_for_service_acl($host, $service);
	my $res = $ldap->search(
		base => $dn,
		scope => "base",
		filter => q(objectClass=*),
		attrs => ["member"]);
	if ($res->is_error) {
		warn ldap_errmsg($res, $dn);
		return undef;
	}
	my @users;
	for my $entry ($res->entries) {
		@users = map {user_from_dn $_} $entry->get_value("member");
	}
	return @users;
}

sub mgmt_access_modify {
	my ($ldap, $host, $service, %args) = @_;
	my $dn = dn_for_service_acl($host, $service);
	my $addusers = $args{add} // [];
	my $delusers = $args{delete} // [];
	my %changes = ();
	if (@$addusers) {
		$changes{add} = {member => [map {dn_for_user $_} @$addusers]};
	}
	if (@$delusers) {
		$changes{delete} = {member => [map {dn_for_user $_} @$delusers]};
	}
	if (%changes) {
		my $res = $ldap->modify($dn, %changes);
		if ($res->is_error) {
			warn ldap_errmsg($res, $dn);
			return undef;
		}
	}
	return 1;
}

$commands{"access"} = sub {
	my (@services, @users, @add, @del);

	for (@_) {
		if (m!/!) {
			my ($h, $s) = parse_hostservice_safe($_);
			if ($s eq 'shell') {
				push @services, [$h, $_] for SHELL_SERVICES;
			} else {
				push @services, [$h, $s];
			}
		} elsif (m!^\+(.+)$!) {
			push @add, $1;
		} elsif (m!^-(.+)$!) {
			push @del, $1;
		} else {
			push @users, $_;
		}
	}

	if ((@add or @del) and @users) {
		warn "Error: Cannot mix 'modify' ([+-]<user>) and 'search' (<user>) args\n";
		exit ERR_SYNTAX;
	}
	elsif (@add or @del) {
		$ldap = ldap_connect_auth;
		for (@services) {
			my ($host, $service) = @$_;
			my @users = mgmt_access_get_users($ldap, $host, $service);
			my @add_svc = grep {not $_ ~~ @users} @add;
			my @del_svc = grep {$_ ~~ @users} @del;
			if (@add_svc or @del_svc) {
				say "$host/$service ".join(" ",
					(map {"+$_"} @add_svc),
					(map {"-$_"} @del_svc));
				mgmt_access_modify($ldap, $host, $service,
					add => \@add_svc, delete => \@del_svc);
			} else {
				say "$host/$service: nothing to do";
			}
		}
	}
	elsif (@users) {
		$ldap = ldap_connect_anon;
		my @filters = map {"(member=$_)"} map {user_dn($_)} @users;
		my $filter = "(&(objectClass=groupOfNames)(|".join("", @filters)."))";
		my @results;
		if (@services) {
			for (@services) {
				my ($host, $service) = @$_;
				my $base;
				if ($service eq 'all') {
					$base = dn_for_server($host);
				} else {
					$base = dn_for_service_acl($host, $service);
				}
				status "Searching in ACL: $host/$service";
				my $res = $ldap->search(
						base => $base,
						scope => "sub",
						filter => $filter,
						attrs => ["cn", "member"]);
				ldap_check($res, $base) or next;
				push @results, $res->entries;
				done;
			}
		} else {
			my $base = "ou=servers,dc=cluenet,dc=org";
			status "Searching in all server ACLs...";
			my $res = $ldap->search(
					base => $base,
					scope => "sub",
					filter => $filter,
					attrs => ["cn", "member"]);
			ldap_check($res, $base) or next;
			push @results, $res->entries;
			done;
		}
		# Certainly not pretty, but this will do.
		my @output;
		for my $entry (@results) {
			my $host = server_from_dn($entry->dn);
			my $service = ($entry->get_value("cn"))[0];
			push @output,
				map {{host => $host, service => $service, user => $_}}
				grep {$_ ~~ @users}
				map {user_from_dn($_)}
				$entry->get_value("member");
		}
		printf("%-30s\t%s\n",
			$_->{host}."/".$_->{service}, $_->{user}) for
			sort {$a->{user} cmp $b->{user}}
			sort {$a->{host} cmp $b->{host}}
			sort {$a->{service} cmp $b->{service}}
			@output;

	}
	elsif (@services) {
		$ldap = ldap_connect_anon;
		for (@services) {
			my ($host, $service) = @$_;
			my @users = mgmt_access_get_users($ldap, $host, $service);
			print "$host/$service\t$_\n" for sort @users;
		}
	}
	else {
		usage("access <host/service>... [+-]<user>...") unless @services;
	}
};

$commands{"acl"} = sub {
	usage("acl <host>... [+-]<service>...") unless @_;

	my (@hosts, @add, @del);
	my $err = 0;

	for (@_) {
		when (m!^\+(.+)$!) {
			push @add, $1;
		}
		when (m!^-(.+)$!) {
			push @del, $1;
		}
		default {
			push @hosts, $_;
		}
	}

	if (@add or @del) {
		$ldap = ldap_connect_auth;
		for my $host (@hosts) {
			for my $service (@add) {
				my ($res, $dn, %entry);

				print "Creating ACL: $host/$service\n";

				$dn = "cn=$service,cn=svcAccess,".server_dn($host);
				%entry = (
					objectClass => ["groupOfNames"],
					cn => $service,
					member => [user_dn(whoami)],
				);
				$res = $ldap->add($dn, attr => [%entry]);
				if ($res->is_error) { warn ldap_errmsg($res, $dn); $err++; }

				$dn = server_dn($host);
				%entry = (
					authorizedService => $service,
				);
				$res = $ldap->modify($dn, add => \%entry);
				if ($res->is_error) { warn ldap_errmsg($res, $dn); $err++; }
			}
			for my $service (@del) {
				my ($dn, $res, %entry);

				print "Deleting ACL: $host/$service\n";

				$dn = "cn=$service,cn=svcAccess,".server_dn($host);
				$res = $ldap->delete($dn);
				if ($res->is_error) { warn ldap_errmsg($res, $dn); $err++; }

				$dn = server_dn($host);
				%entry = (
					authorizedService => $service,
				);
				$res = $ldap->modify($dn, delete => \%entry);
				if ($res->is_error) { warn ldap_errmsg($res, $dn); $err++; }
			}
		}
	}
	else {
		$ldap = ldap_connect_anon;
		for my $host (@hosts) {
			# authz = authorizedService attributes
			# acl = cn=svcAccess subentry
			my ($res, %services, @svcs_authz, @svcs_acl);

			my $dn = server_dn($host);
			$res = $ldap->search(base => $dn, scope => "base",
				filter => q(objectClass=*), attrs => ["authorizedService"]);
			if ($res->is_error) { warn ldap_errmsg($res, $dn); $err++; }

			for my $entry ($res->entries) {
				push @svcs_authz, $entry->get_value("authorizedService");
			}

			$dn = "cn=svcAccess,".server_dn($host);
			$res = $ldap->search(base => $dn, scope => "one",
				filter => q(objectClass=groupOfNames), attrs => ["cn"]);
			if ($res->is_error) { warn ldap_errmsg($res, $dn); $err++; }

			for my $entry ($res->entries) {
				push @svcs_acl, $entry->get_value("cn");
			}

			# create a hash of $service => [has_authz, has_acl]
			%services = map {$_ => [1, undef]} @svcs_authz;
			$services{$_}->[1] = 1 for @svcs_acl;

			for my $svc (sort keys %services) {
				my ($has_authz, $has_acl) = @{$services{$svc}};
				if (!$has_authz) {
					warn "warning: service '$svc' not listed as authorizedService\n";
				}
				if (!$has_acl) {
					warn "warning: service '$svc' does not have an ACL\n";
				}
				print "$host/$svc\n";
			}
		}
	}

	return $err;
};

$commands{"server"} = sub {
	my (@hosts, @changes);

	for (@_) {
		when (/=/) {
			push @changes, $_;
		}
		default {
			push @hosts, $_;
		}
	}

	if (!@hosts) {
		$ldap = ldap_connect_anon;

		my $base = "ou=servers,dc=cluenet,dc=org";
		my $res = $ldap->search(base => $base, scope => "one",
			filter => q(objectClass=server), attrs => ["cn"]);
		ldap_check($res, $base);

		my @hosts = sort grep {s/\.cluenet\.org$//i}
					map {($_->get_value("cn"))[0]}
					$res->entries;

		say for @hosts;
	}
	elsif (@changes) {
		my $changes = parse_changelist(\@changes,
			translate => sub {
				my ($attr, $value) = @_;
				given ($attr) {
					when ("active") {
						$attr = "isActive";
						$value = ldap_bool($value);
					}
					when ("official") {
						$attr = "isOfficial";
						$value = ldap_bool($value);
					}
					when ("public") {
						$attr = "userAccessible";
						$value = ldap_bool($value);
					}
					when ("admin") {
						$value = user_dn($value);
					}
					when ("owner") {
						$value = user_dn($value);
					}
				}
				return ($attr, $value);
			});
		modify_objects($changes, map {server_dn($_)} @hosts);
		ldap_exit;
	}
	else {
		$ldap = ldap_connect_anon;
		for my $host (@hosts) {
			print_server_info($host);
		}
	}
};

$commands{"server:dump"} = sub {
	ext_dump_ldif(base => server_dn($_), scope => "sub") for @_;
	return 0;
};

$commands{"server:admin"} = sub {
	my (@hosts, @add, @del);

	my $show_princ = 0;

	Getopt::Long::GetOptionsFromArray(\@_,
		"p|principal" => \$show_princ);

	for (@_) {
		when (/^\+(.+)$/)	{ push @add, user_from_dn($1) }
		when (/^-(.+)$/)	{ push @del, user_from_dn($1) }
		default			{ push @hosts, $_ }
	}

	usage("server:admin <host>... [+-]<user>...") unless @hosts;

	if (@add or @del) {
		# update admins
		$ldap = ldap_connect_auth;
		for my $host (@hosts) {
			my $dn = server_dn($host);
			my $res = $ldap->search(base => $dn, scope => "base",
				filter => q(objectClass=server),
				attrs => ["admin", "owner"]);
			ldap_check($res, $dn) or next;

			my ($owner, @admins, %changes);
			for my $entry ($res->entries) {
				$owner = user_from_dn($entry->get_value("owner"));
				@admins = map {user_from_dn $_}
					$entry->get_value("admin");
			}

			if ($owner ~~ @del and !($owner ~~ @admins)) {
				warn "$host: Cannot remove server owner $owner\n";
			}

			my @add_host = grep {not $_ ~~ @admins} @add;
			my @del_host = grep {$_ ~~ @admins} @del;

			if ($owner ~~ @add_host) {
				warn "$host: Adding server owner $owner as an admin\n";
			}

			if (@add_host) {
				$changes{add} = {
					admin => [map {user_dn $_} @add_host],
				};
				print "$host: Adding ".join(", ", @add_host)."\n";
			}
			if (@del_host) {
				$changes{delete} = {
					admin => [map {user_dn $_} @del_host],
				};
				print "$host: Removing ".join(", ", @del_host)."\n";
			}

			if (@add_host or @del_host) {
				my $res = $ldap->modify($dn, %changes);
				ldap_check($res, $dn);
			} else {
				print "$host: Nothing to do\n";
			}
		}
	} else {
		# list admins
		$ldap = ldap_connect_anon;
		for my $host (@hosts) {
			my $dn = server_dn($host);
			my $res = $ldap->search(base => $dn, scope => "base",
				filter => q(objectClass=server),
				attrs => ["admin", "owner"]);
			ldap_check($res, $dn) or next;

			for my $entry ($res->entries) {
				my $owner = $entry->get_value("owner");
				my @admins = $entry->get_value("admin");

				$owner = user_from_dn $owner;
				@admins = sort map {user_from_dn $_} @admins;

				if ($show_princ) {
					$owner =~ s/$/\@CLUENET.ORG/;
					@admins =
						grep {/@/ or s/$/\@CLUENET.ORG/}
						grep {s/(@)(.+)/$1.uc($2)/e or 1}
						@admins;
				}

				my $fmt = "%-15s\t%s\n";
				printf $fmt, $host, $owner.' (owner)';
				printf $fmt, $host, $_ for @admins;
			}
		}
	}
};

$commands{"server:create"} = sub {
	my ($res, %entry);

	my $host = shift;
	my $owner;
	my @address;

	Getopt::Long::GetOptionsFromArray(\@_,
		"o|owner=s" => \$owner,
		"a|address=s" => \@address);

	usage("server:create <hostname>") unless $host;

	$ldap = ldap_connect_auth;
	$owner //= whoami;
	@address || push @address, "0.0.0.0";

	my $fqdn = make_server_fqdn($host);
	print "Creating server: $fqdn (owned by $owner)\n";

	# add main server entry
	my $dn = server_dn($host);
	%entry = (
		objectClass => ["server",
			"ipHost",
			"authorizedServiceObject",
			"serviceRequirementObject"],
		cn => $fqdn,
		owner => user_dn($owner),
		isActive => "TRUE",
		isOfficial => "FALSE",
		userAccessible => "TRUE",
		authorizedService => [SHELL_SERVICES],
		ipHostNumber => \@address,
	);
	print "+ $dn\n";
	$res = $ldap->add($dn, attr => [%entry]);
	if ($res->is_error) { die ldap_errmsg($res, $dn); }

	# add svcAccess subtree
	my $subdn = "cn=svcAccess,$dn";
	%entry = (
		objectClass => ["groupOfGroups"],
		cn => "svcAccess",
	);
	print "+ $subdn\n";
	$res = $ldap->add($subdn, attr => [%entry]);
	ldap_check($res, $subdn);

	# add service groups
	for (SHELL_SERVICES) {
		$subdn = "cn=$_,cn=svcAccess,$dn";
		%entry = (
			objectClass => ["groupOfNames"],
			cn => $_,
			member => user_dn($owner),
		);
		print "+ $subdn\n";
		$res = $ldap->add($subdn, attr => [%entry]);
		ldap_check($res, $subdn);
	}

	return $err;
};

$commands{"server:delete"} = sub {
	my ($res);

	usage("server:delete <hostname>...") unless @_;

	$ldap = ldap_connect_auth;

	for my $host (@_) {
		my $fqdn = make_server_fqdn($host);
		print "Deleting server: $fqdn\n";

		my $dn = server_dn($fqdn);
		$res = $ldap->search(base => $dn, scope => "sub",
			filter => q(objectClass=*), attributes => ["dn"]);
		$res->is_error and die ldap_errmsg($res, $dn);

		my @children = reverse map {$_->dn} $res->entries;
		for my $subdn (@children) {
			print "- $subdn\n";
			$res = $ldap->delete($subdn);
			ldap_check($res, $subdn);
		}
	}

	return $err;
};

$commands{"user"} = sub {
	my (@users, @changes);

	for (@_) {
		when (/=/) {
			push @changes, $_;
		}
		default {
			push @users, $_;
		}
	}

	if (!@users) {
		$ldap = ldap_connect_anon;

		my $base = "ou=people,dc=cluenet,dc=org";
		my $res = $ldap->search(base => $base, scope => "one",
			filter => q(objectClass=posixAccount), attrs => ["uid"]);
		ldap_check($res, $base);

		my @users = sort map {($_->get_value("uid"))[0]} $res->entries;
		say for @users;
	}
	elsif (@changes) {
		my $changes = parse_changelist(\@changes);
		modify_objects($changes, map {user_dn($_)} @users);
		ldap_exit;
	}
	else {
		$ldap = ldap_connect_anon;
		for my $user (@users) {
			print_user_info(get_user_info($user));
		}
	}

	return 0;
};

$commands{"user:dump"} = sub {
	ext_dump_ldif(base => user_dn($_), scope => "sub") for @_;
	return 0;
};

$commands{"user:chsh"} = sub {
	my $shell = shift;

	if ($shell && $shell eq '-s') {
		$shell = shift;
	}

	if (defined $shell) {
		if (!$shell) {
			die "error: shell not specified\n";
		} elsif ($shell !~ m!^/.+!) {
			die "error: shell must be an absolute filesystem path\n";
		} elsif ($shell !~ m!^/bin/(ba)?sh$!) {
			warn "warning: shell might not be available on other servers\n";
		}

		$ldap = ldap_connect_auth;
		my $dn = user_dn(whoami);
		my %entry = (loginShell => $shell);

		print "Changing shell for ".whoami." to $shell\n";
		my $res = $ldap->modify($dn, replace => \%entry);
		$res->is_error and die ldap_errmsg($res, $dn);
	}
	else {
		$ldap = ldap_connect_auth;
		my $dn = user_dn(whoami);

		my $res = $ldap->search(base => $dn, scope => "base",
			filter => q(objectClass=posixAccount));
		$res->is_error and die ldap_errmsg($res, $dn);

		for my $entry ($res->entries) {
			$shell = $entry->get_value("loginShell");
		}
		$shell //= "not set.";
		print "Your current shell is $shell\n";
	}
};

$commands{"help"} = sub {
	# suck it, iso8859
	print qq¶Usage: cluenet <command>

ACCESS LISTS
	show	access <host/service>...
	grant	access <host/service>... [+-]<user>...
	list	acl <host>...
	create	acl <host>... [+-]<user>...

USER ACCOUNTS
	view	user <user>...
	shell	user:chsh <shell>

SERVERS
	show	server [<host>...]
	modify  server <host>... <attr>{=,+=,-=}<value>...
	admins	server:admin <host>... [[+-]<user>...]
	add	server:create <host> [--owner <owner>]

MISCELLANEOUS
	whoami
¶;

	return 0;
};

$commands{"whoami"} = sub {
	my $verbose = 0;

	Getopt::Long::GetOptionsFromArray(\@_,
		"v|verbose" => \$verbose);

	$ldap = ldap_connect_auth;
	my $resp = $ldap->who_am_i->response;
	my $dn;
	my $uid;
	my $princ;
	given ($resp) {
		when (/^dn:(uid=(.+),ou=people,dc=cluenet,dc=org)$/) {
			$dn = $1;
			$uid = $2;
		}
		when (/^dn:(uid=(.+),ou=people,dc=nullroute,dc=eu,dc=org)$/) {
			$dn = $1;
			$uid = $2.'@nullroute.eu.org';
		}
		when (/^u:(.+)$/) {
			$dn = user_dn($1);
			$uid = $1;
		}
		default {
			say "unknown response $resp";
		}
	}
	given ($uid) {
		when (/^(.+)@(.+?)$/) {
			my $query = Net::DNS::Resolver->new->query("_kerberos.$2", "TXT");
			if ($query) {
				my $realm = (($query->answer)[0]->char_str_list)[0];
				say "Foreign user \"$1\" from realm $realm" if !$verbose;
				$princ = "$1\@$realm";
			}
			else {
				say "Foreign user \"$1\" from $2" if !$verbose;
				$princ = "$1\@$2";
			}
		}
		default {
			say "Cluenet user \"$uid\"" if !$verbose;
			$princ = "$uid\@CLUENET.ORG";
		}
	}
	if ($verbose) {
		say "LDAP authzid: $resp";
		say "LDAP DN:      $dn";
		say "Kerberos:     $princ";
	} else {
		say "dn: $dn";
	}
	return 0;
};

### Main code

$cmd = shift(@ARGV) // "help";

if (defined $commands{$cmd}) {
	$exit = $commands{$cmd}->(@ARGV);
	$ldap and $ldap->unbind;
	exit (defined $exit && $exit) ? 1 : 0;
} else {
	die "Unknown command '$cmd'\n";
}
