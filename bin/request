#!/usr/bin/env perl
use warnings;
use strict;
use feature "say";
use feature "switch";
use Cluenet::Common;
use Cluenet::Rpc;
use Cluenet::Rpc::Client;
use Data::Dumper;
use Getopt::Long qw(:config bundling no_ignore_case);

use constant {
	COMMAND_DIR	=> "/home/grawity/cluenet/cluerpc-client/*.pl",
};

my %commands;
my $debug;
my $host;
my $port;
my $rpc;
our $user;
my $verbose;
my $yes;

sub confirm {
	unless ($yes) {
		my $text = shift;
		print "$text [yn] ";
		exit unless <STDIN> =~ /^y/i;
	}
}

sub authenticate	{ $rpc->authenticate(@_); }
sub request		{ $rpc->request(@_); }

sub check {
	my $r = shift;
	if (!$r->{status}) {
		if ($r->{error}) {
			chomp(my $err = join("\n", $r->{error}));
			warn "$err\n";
		}
		die "\033[1;31mError: ".($r->{msg} // "unknown error")."\033[m\n";
	}
}

# load available commands

for my $file (glob(COMMAND_DIR)) {
	my ($name, $command) = do $file;
	if ($@) {
		warn "Loading $file failed: $@\n";
	}
	$commands{$name} = $command;
}

$user = $ENV{USER};
$host = getfqdn;
$port = 10875;
$debug = $ENV{DEBUG} || 0;

GetOptions(
	"D|debug!"	=> \$debug,
	"h|host=s"	=> \$host,
	"v|verbose!"	=> \$verbose,
	"y|yes!"	=> \$yes,
	"z|authz=s"	=> \$user,
);

# connect to RPC server

$rpc = Cluenet::Rpc::Client->new;
$rpc->{callbacks} = {
	user => \$user,
};
$rpc->{verbose} = $verbose;
$rpc->{debug} = $debug;

$rpc->connect($host, $port);

# run requests

my $name = shift(@ARGV) // "help";
my $r;

given ($name) {
	when ("help") {
		check $r = request(cmd => "list");
		say "Usage:";
		say "";
		for my $name (sort keys %commands) {
			my $cmd = $commands{$name};
			if (grep {!($_ ~~ @{$r->{commands}})} @{$cmd->{requires}}) {
				next;
			}

			if (defined $cmd->{usage}) {
				say "request $cmd->{usage}";
			} else {
				next;
			}

			if (defined $cmd->{description}) {
				say "\t$cmd->{description}";
			}

			say "";
		}
	}
	when ("raw") {
		check $r = authenticate;
		while (<STDIN>) {
			my %args = eval $_;
			$r = request(%args);
			print Dumper($r);
		}
	}
	when (defined $commands{$name}) {
		$commands{$name}{command}->($name, $rpc);
	}
	default {
		die "Unknown command.\n";
	}
}
