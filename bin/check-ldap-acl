#!/usr/bin/env perl
# check-ldap-acl - check if given user is listed in the LDAP access
#   list for (this_host, given_service).
# Cluenet specific.

use warnings;
use strict;
use Net::LDAP;
use Sys::Hostname;

my $ldap_server;

sub usage {
	warn "Usage: $0 <username> [<service>]\n";
	exit 2;
}

sub getfqdn {
	return (gethostbyname(shift // hostname))[0];
}

sub check {
	my ($user, $service) = @_;

	my $fqdn = getfqdn();

	my $user_dn = "uid=$user,ou=people,dc=cluenet,dc=org";
	my $acl_dn = "cn=$service,cn=svcAccess,cn=$fqdn,ou=servers,dc=cluenet,dc=org";

	my $ldap = Net::LDAP->new($ldap_server, onerror => "die") or die "$!";

	$ldap->bind;

	my $res = $ldap->search(
		base => $acl_dn,
		scope => "base",
		filter => q(objectClass=*),
		attrs => ["member"]);

	for my $entry ($res->entries) {
		my @members = $entry->get_value("member");
		return 1 if $user_dn ~~ @members;
	}
	return 0;
}

if (-e "/etc/cluenet/has-ldap-mirror") {
	$ldap_server = "ldap://localhost";
} else {
	$ldap_server = "ldap://ldap.cluenet.org";
}

my $user = shift(@ARGV) // usage();
my $service = shift(@ARGV) // "other";

exit(check($user, $service) ? 0 : 1);
