#!/usr/bin/perl
# Checks if given user is listed in the LDAP access list for a service on this host.
# <grawity@gmail.com>

use strict;
use Net::LDAP;
use Sys::Hostname;
use constant {
	LDAP_SERVER	=> "ldap://localhost:3890",
	DEFAULT_SERVICE	=> "other",
};

sub usage {
	warn "Usage: $0 <username> [<service>]\n";
	exit 2;
}

sub getfqdn {
	return (gethostbyname(shift // hostname))[0];
}

sub check {
	my ($user, $service) = @_;

	my $fqdn = getfqdn();

	my $user_dn = "uid=$user,ou=people,dc=cluenet,dc=org";
	my $acl_dn = "cn=$service,cn=svcAccess,cn=$fqdn,ou=servers,dc=cluenet,dc=org";

	my $ldap = Net::LDAP->new(LDAP_SERVER, onerror => "die") or die "$!";

	$ldap->bind;

	my $res = $ldap->search(
		base => $acl_dn,
		scope => "base",
		filter => q(objectClass=*),
		attrs => ["member"]);

	for my $entry ($res->entries) {
		my @members = $entry->get_value("member");
		return 1 if $user_dn ~~ @members;
	}
	return 0;
}

my $user = shift(@ARGV) // usage();
my $service = shift(@ARGV) // DEFAULT_SERVICE;

exit(check($user, $service) ? 0 : 1);
