#!/usr/bin/env perl
# chsh - selects between shadow 'chsh' and Cluenet 'chsh'.
# Cluenet specific.

use warnings;
use strict;
use English;
use Getopt::Long qw(:config no_ignore_case);
use User::pwent;

# TODO: Find directory
my $CLUENET_DIR = "/cluenet";

my $real_chsh = "/usr/bin/chsh.real";

sub usage {
	warn "Usage: $0 [-s <shell>]\n";
	exit 2;
}

sub do_chsh {
	my $shell = shift;
	my @cmd;
	if ($UID >= 25000) {
		@cmd = ("$CLUENET_DIR/bin/cn", "user:chsh");
	} else {
		@cmd = ($real_chsh, "-s");
	}
	exec(@cmd, $shell) or die "exec: $!";
}

my $shell;

GetOptions("s|shell=s" => \$shell) or usage();

if ($shell) {
	do_chsh($shell);
} else {
	my $pw = getpwuid($UID);
	print "Your current shell is ".$pw->shell."\n";
	print "Enter the new value, or press ENTER for the default\n";
	printf "%19s [%s]: ", "Login Shell", $pw->shell;
	chomp(my $input = <STDIN>);
	if ($input) {
		do_chsh($input);
	}
}
