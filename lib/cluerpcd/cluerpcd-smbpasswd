#!/usr/bin/env perl
use common::sense;
use Cluenet::Common;
use Cluenet::Rpc::Server;

# TODO would it be better to just rely on 'rpc -z <user>'
# instead of k5userok here?

sub authorize {
	use Cluenet::Kerberos;
	my ($auth, $target) = @_;
	return ($target eq $auth->{user}
		|| $auth->{user} eq 'root'
		|| krb5_kuserok($target, $auth->{authn}));
}

sub reset_password_smb {
	my ($user) = @_;

	my $pw = gen_passwd();

	open my $fd, "|-", ("sudo", "smbpasswd", "-L", "-s", "-a", $user)
		or return {failure,
			error => "internal error: could not run smbpasswd"};
	print $fd $pw."\n".$pw."\n";
	close $fd;

	if ($? == 0) {
		my $fqdn = getfqdn();
		return {success,
			msg => "Use 'smbpasswd' or 'smbpasswd -r $fqdn' to change your password.",
			account =>
				{username => $user,
				password => $pw}};
	} else {
		return {failure,
			error => "internal error: smbpasswd failed"};
	}
}

rpc_helper_main {
	my ($auth, %args) = @_;

	if (!defined $args{user}) {
		return {failure,
			error => "missing parameter"};
	}

	if (!authorize($auth, $args{user})) {
		return {failure,
			error => "access denied: can only modify own account"};
	}

	if (!is_valid_user($args{user})) {
		return {failure,
			error => "access denied: account does not exist"};
	}

	return reset_password_smb($args{user});
};
